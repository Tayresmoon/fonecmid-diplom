
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий	

 
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.ВКМ_ДатаНачала КАК ВКМ_ДатаНачала,
		|	ДоговорыКонтрагентов.ВКМ_ДатаОкончания КАК ВКМ_ДатаОкончания,
		|	ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора,
		|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРаботы КАК ВКМ_СтоимостьЧасаРаботы
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Договор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	ДоговорАбонентскогоОбслуживания = (Выборка.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	
	Если НЕ (ДоговорАбонентскогоОбслуживания И Дата >= Выборка.ВКМ_ДатаНачала И Дата <= КонецДня(Выборка.ВКМ_ДатаОкончания)) Тогда
		Отказ = Истина;
		Если НЕ ДоговорАбонентскогоОбслуживания Тогда
			Сообщение = "Договор не является договором абонентского обслуживания"; 
			ОбщегоНазначения.СообщитьПользователю(Сообщение,,,ЭтотОбъект);
		Иначе
			Сообщение = "Дата документа за границами периода действия договора";
			ОбщегоНазначения.СообщитьПользователю(Сообщение, , , ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, Сотрудник = &Сотрудник) КАК
	|		ВКМ_УсловияОплатыСотрудниковСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Сотрудник", Специалист);
	РезультатЗапроса = Запрос.Выполнить();
	ВКМ_ПроцентОтРабот = 0;
	Если РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		ТекстСообщения = СтрШаблон("Специалисту %1 не назначен процент премии", Специалист);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , ЭтотОбъект);
	Иначе
		ВыборкаПроцентОтРабот = РезультатЗапроса.Выбрать();
		ВыборкаПроцентОтРабот.Следующий();
		ПроцентОтРабот = ВыборкаПроцентОтРабот.ПроцентОтРабот;
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		ВсегоЧасов = Работы.Итог("ЧасыКОплатеКлиенту");
		
		Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Клиент =  Клиент;
		Движение.Договор = Договор; 
		Движение.КоличествоЧасов = ВсегоЧасов;
		Движение.СуммаКОплате = Выборка.ВКМ_СтоимостьЧасаРаботы * ВсегоЧасов;
		
		Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Специалист;
		Движение.ЧасовКОплате = ВсегоЧасов;
		Движение.СуммаКОплате = ВсегоЧасов * Выборка.ВКМ_СтоимостьЧасаРаботы * ПроцентОтРабот / 100;
			
	КонецЕсли;
	
КонецПроцедуры

	
#КонецОбласти

#КонецЕсли	