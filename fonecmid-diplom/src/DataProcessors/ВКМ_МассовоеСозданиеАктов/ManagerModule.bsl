#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СозданиеДокументовНаОснованииДоговоров(МесяцОтчета) Экспорт

	СписокДоговоров = СпискоДоговоровДляАктов(МесяцОтчета);

	Если СписокДоговоров.Пустой() Тогда
		ТекстШаблона = "За выбранный период данных нет";
		ОбщегоНазначения.СообщитьПользователю(ТекстШаблона);
		Возврат ТекстШаблона;
	Иначе
		ТаблицаДоговоров = СписокДоговоров.Выгрузить();
		МассивДанных = СоздатьДокументыРеализацияТоваровИУслуг(ТаблицаДоговоров, МесяцОтчета);
	КонецЕсли;

	Возврат МассивДанных;

КонецФункции	

Функция СпискоДоговоровДляАктов(МесяцОтчета)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Договор КАК Договор
	|ПОМЕСТИТЬ ВТ_ДокументРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&МесяцОтчета, МЕСЯЦ) И КОНЕЦПЕРИОДА(&МесяцОтчета, МЕСЯЦ)
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВКМ_ДатаОкончания >= НАЧАЛОПЕРИОДА(&МесяцОтчета, МЕСЯЦ)
	|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления
	|	И НЕ ДоговорыКонтрагентов.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_ДокументРеализации.Договор КАК Договор
	|				ИЗ
	|					ВТ_ДокументРеализации КАК ВТ_ДокументРеализации)
	|	И ДоговорыКонтрагентов.ВКМ_ДатаНачала <= КОНЕЦПЕРИОДА(&МесяцОтчета, МЕСЯЦ)";

	Запрос.УстановитьПараметр("МесяцОтчета", МесяцОтчета);

	Возврат Запрос.Выполнить();

КонецФункции

Функция СоздатьДокументыРеализацияТоваровИУслуг(ТаблицаДоговоров, МесяцОтчета)

	МассивДанных = Новый Массив;

	Для Каждого Документ Из ТаблицаДоговоров Цикл

		ДокументМененджер = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ДатаДокумента = ТекущаяДатаСеанса();
		ДокументМененджер.Дата = ?(ДатаДокумента > КонецМесяца(МесяцОтчета), КонецМесяца(МесяцОтчета), ДатаДокумента);
		ДокументМененджер.Организация = Документ.Договор.Организация;
		ДокументМененджер.Контрагент = Документ.Договор.Владелец;
		ДокументМененджер.Ответственный = Пользователи.ТекущийПользователь();
		ДокументМененджер.Основание = Документ.Договор;
		ДокументМененджер.Комментарий = СтрШаблон("Услуги за период с %1 по %2", Формат(НачалоМесяца(МесяцОтчета),
			"ДФ=dd.MM.yyyy;"), Формат(КонецМесяца(МесяцОтчета), "ДФ=dd.MM.yyyy;"));
		ЗаполнитьЗначенияСвойств(ДокументМененджер, Документ);
		ДокументМененджер.Записать(РежимЗаписиДокумента.Запись);
		ДокументМененджер.ВКМ_ВыполнитьАвтозаполнение();
		ДокументМененджер.Записать(РежимЗаписиДокумента.Запись);
		Если ДокументМененджер.ПроверитьЗаполнение() Тогда
			ДокументМененджер.Записать(РежимЗаписиДокумента.Проведение);
		Иначе
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Не проведен. %1 не прошел проверку на заполнение",
				ДокументМененджер.Ссылка));
		КонецЕсли;

		МассивДанных.Добавить(ДокументМененджер.Ссылка);

	КонецЦикла;

	Возврат МассивДанных;

КонецФункции

#КонецОбласти

#КонецЕсли